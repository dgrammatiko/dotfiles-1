#!/usr/bin/env zsh

# ==== CONDA ==== #

path=(
  $HOME/conda/bin
  $path
)

conda activate base

# ==== autoloads ==== #

autoload -Uz zmv
autoload -Uz zargs
autoload -z edit-command-line
zle -N edit-command-line
bindkey "^[e" edit-command-line

# ==== iTerm2 shell integration ==== #

test -e "${HOME}/.iterm2_shell_integration.zsh" &&\
  source "${HOME}/.iterm2_shell_integration.zsh"

# ==== fuzzy finder in the terminal: fzf ==== #

# https://github.com/junegunn/fzf

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

if (( $+commands[fd] )); then

  # Setting fd as the default source for fzf
  export FZF_DEFAULT_COMMAND='fd --type f'

  # To apply the command to CTRL-T as well
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

  # Use fd (https://github.com/sharkdp/fd) instead of the default find
  # command for listing path candidates.
  # - The first argument to the function ($1) is the base path to start traversal
  # - See the source code (completion.{bash,zsh}) for the details.
  _fzf_compgen_path() {
    fd --hidden --follow --exclude ".git" . "$1"
  }

  # Use fd to generate the list for directory completion
  _fzf_compgen_dir() {
    fd --type d --hidden --follow --exclude ".git" . "$1"
  }

fi

# ==== Save dirstack history to .zdirs ==== #

# https://github.com/grml/grml-etc-core/blob/master/etc/zsh/zshrc

DIRSTACKSIZE=${DIRSTACKSIZE:-20}
dirstack_file=${dirstack_file:-${HOME}/.zdirs}

if [[ -f ${dirstack_file} ]] && [[ ${#dirstack[*]} -eq 0 ]] ; then
  dirstack=( ${(f)"$(< $dirstack_file)"} )
  # "cd -" won't work after login by just setting $OLDPWD, so
  [[ -d $dirstack[1] ]] && cd $dirstack[1] && cd $OLDPWD
fi

chpwd_functions+=(chpwd_dirpersist)
chpwd_dirpersist() {
  if (( $DIRSTACKSIZE <= 0 )) || [[ -z $dirstack_file ]]; then return; fi
  local -ax my_stack
  my_stack=( ${PWD} ${dirstack} )
  builtin print -l ${(u)my_stack} >! ${dirstack_file}
}
